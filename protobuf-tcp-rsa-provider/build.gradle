plugins {
    id 'java'
    id 'maven'
    id "com.google.protobuf" version "0.8.3"
    id "org.dm.bundle" version "0.10.0"
    id "com.jfrog.bintray" version "1.6"
}

sourceCompatibility = 1.8
description = 'TCP/Protobuffer implementation of Aries RSA DistributionProvider'

dependencies {
    compile 'org.apache.aries.rsa:org.apache.aries.rsa.spi:1.11.0'
    compile 'com.google.protobuf:protobuf-java:3.4.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compileOnly 'org.osgi:org.osgi.core:4.3.1'
    compileOnly 'org.osgi:org.osgi.service.remoteserviceadmin:1.1.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/source/proto/main/java"
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
}

bundle {
    failOnError = true
    instructions << [
            'Export-Package'       : 'com.athaydes.osgi.rsa.provider.protobuf.api',
            'Import-Package'       : 'org.osgi.service.remoteserviceadmin;version=1.0.0,*',
            'Bundle-Vendor'        : 'Renato Athaydes',
            'Bundle-Name'          : 'Protobuffer TCP RSA Provider',
            'Bundle-Description'   : description,
            'Bundle-DocURL'        : 'https://github.com/renatoathaydes/protobuf-tcp-rsa-provider',
            'Bundle-Activator'     : 'com.athaydes.osgi.rsa.provider.protobuf.Activator',
            'Provide-Capability'   : 'osgi.remoteserviceadmin.distribution;' +
                    'configs:List<String>="com.athaydes.protobuf"; version:Version=1.0'
    ]
}

tasks.withType(Javadoc) {
    exclude('**/Api.java') // auto-generated Java code does not have proper javadocs
}

/* Publishing config */

task javadocJar( type: Jar ) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar( type: Jar ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure( install.repositories.mavenInstaller ) {
    pom.project {
        inceptionYear '2017'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/protobuf-tcp-rsa-provider'

        scm {
            connection 'git@github.com:renatoathaydes/protobuf-tcp-rsa-provider.git'
            developerConnection 'git@github.com:renatoathaydes/protobuf-tcp-rsa-provider.git'
            url 'https://github.com/renatoathaydes/protobuf-tcp-rsa-provider'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

def getProjectProperty = { String propertyName ->
    project.properties[ propertyName ]
}

bintray {
    user = getProjectProperty "bintrayUserName"
    key = getProjectProperty "bintrayApiKey"
    configurations = [ 'archives' ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'protobuf-tcp-rsa-provider'
        desc = project.description
        vcsUrl = 'https://github.com/renatoathaydes/protobuf-tcp-rsa-provider.git'
        websiteUrl = 'https://github.com/renatoathaydes/protobuf-tcp-rsa-provider'
        issueTrackerUrl = 'https://github.com/renatoathaydes/protobuf-tcp-rsa-provider/issues'
        licenses = [ 'Apache-2.0' ]
        labels = [ 'osgi', 'java', 'distributed', 'protobuffers', 'tcp' ]
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = getProjectProperty 'ossrhUsername'
                password = getProjectProperty 'ossrhPassword'
                close = '1' // '0' to NOT close
            }
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar